; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!


; Other #define
#define MyAppExeName "LitePM.exe"
#define MyAppName "Lite Process Manager"
#define MyAppPublisher ""
#define MyAppURL ""
#define MyAppVersion "1.0.1"


[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{EFD64A45-12DC-4429-853F-10B453B90F0A}

AppName={#MyAppName}
AppVerName={#MyAppName} {#MyAppVersion}
AppVersion={#= MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
InfoBeforeFile=..\..\LitePM\bin\BuildZipRelease\Bin\README.txt
OutputBaseFilename=LitePM-v{#MyAppVersion}-Setup
SetupIconFile=..\..\LitePM\icon.ico
Compression=lzma/ultra64
SolidCompression=yes
VersionInfoCompany=
VersionInfoDescription=A lightweight Process Manager alternative
VersionInfoProductName=LitePM
VersionInfoVersion={#MyAppVersion}
OutPutDir=..\..\RELEASE\

[Languages]
Name: english; MessagesFile: compiler:Default.isl

[Tasks]
Name: desktopicon; Description: {cm:CreateDesktopIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked
Name: quicklaunchicon; Description: {cm:CreateQuickLaunchIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked
Name: replaceTaskmgr; Description: {cm:ReplaceTaskmgr}; GroupDescription: {cm:OtherTasks}; Check: DoesLitePMReplaceTaskmgr(); Flags: unchecked dontinheritcheck
Name: restoreTaskmgr; Description: {cm:RestoreTaskmgr}; GroupDescription: {cm:OtherTasks}; Check: NOT DoesLitePMReplaceTaskmgr(); Flags: unchecked dontinheritcheck

[Files]
Source: ..\..\LitePM\bin\BuildZipRelease\Bin\changelog.txt; DestDir: {app}; Flags: ignoreversion
Source: ..\..\LitePM\bin\BuildZipRelease\Bin\KernelMemory.sys; DestDir: {app}; Flags: ignoreversion; Check: NOT Is64BitInstallMode()
Source: ..\..\LitePM\bin\BuildZipRelease\Bin\launch server.bat; DestDir: {app}; Flags: ignoreversion
Source: ..\..\LitePM\bin\BuildZipRelease\Bin\license.rtf; DestDir: {app}; Flags: ignoreversion
Source: ..\..\LitePM\bin\BuildZipRelease\Bin\README.txt; DestDir: {app}; Flags: ignoreversion
Source: ..\..\LitePM\bin\BuildZipRelease\Bin\{#MyAppExeName}; DestDir: {app}; Flags: ignoreversion
Source: ..\..\LitePM\bin\BuildZipRelease\Bin\Help\*; DestDir: {app}\Help\; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: {group}\{#MyAppName}; Filename: {app}\{#MyAppExeName}
Name: {group}\{cm:ProgramOnTheWeb,{#MyAppName}}; Filename: {#MyAppURL}
Name: {group}\{cm:UninstallProgram,{#MyAppName}}; Filename: {uninstallexe}
Name: {commondesktop}\{#MyAppName}; Filename: {app}\{#MyAppExeName}; Tasks: desktopicon
Name: {group}\{cm:StartServer}; Filename: {app}\launch server.bat; WorkingDir: {app}

[Registry]
Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows NT\CurrentVersion\Image File Execution Options\taskmgr.exe; Flags: uninsdeletekeyifempty dontcreatekey
Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows NT\CurrentVersion\Image File Execution Options\taskmgr.exe; ValueType: string; ValueName: Debugger; ValueData: """{app}\{#MyAppExeName}"""; Tasks: replaceTaskmgr
Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows NT\CurrentVersion\Image File Execution Options\taskmgr.exe; ValueType: string; ValueName: Debugger; ValueData: """{app}\{#MyAppExeName}"""; Flags: uninsdeletevalue; Check: NOT DoesLitePMReplaceTaskmgr()
Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows NT\CurrentVersion\Image File Execution Options\taskmgr.exe; ValueName: Debugger; Tasks: restoreTaskmgr; Flags: deletevalue uninsdeletevalue; Check: NOT DoesLitePMReplaceTaskmgr()

[Run]
Filename: {app}\{#MyAppExeName}; Description: {cm:LaunchProgram,{#MyAppName}}; Flags: nowait postinstall skipifsilent
Filename: {win}\Microsoft.NET\Framework\v2.0.50727\ngen.exe; Parameters: "install ""{app}\{#MyAppExeName}"""; StatusMsg: {cm:Optimization}; Flags: runhidden runascurrentuser skipifdoesntexist

[Code]

// LitePM replaces taskmgr ?
function DoesLitePMReplaceTaskmgr(): Boolean;
var
  keyValue: String;
begin
	Result := True;
	if RegQueryStringValue(HKLM, 'SOFTWARE\Microsoft\Windows NT\CurrentVersion\Image File Execution Options\taskmgr.exe', 'Debugger', keyValue) then
	begin
		if keyValue = (ExpandConstant('"{app}\{#MyAppExeName}"')) then
		begin
			Result := False;
		end;
	end;
end;

// Initialization of setup
// Check if .Net framework 2.0 is present
function InitializeSetup(): Boolean;
var
	res : Boolean;
	errRes: Integer;
	dotNot2dot0Installed : Boolean;
	winDir : String;
begin
	try
		// An exception will be raised if an attempt is made to expand this constant on a system with no .NET Framework version 2.0 present
		// dotnet48 = dotnet4832 if system is 32-bit, and = dotnet4864 if 64-bit
		ExpandConstant('{dotnet48}');
		Result := True;
	except
		begin
			res := MsgBox(ExpandConstant('{cm:noDotNetInstalled}'), mbCriticalError, MB_YESNO or MB_DEFBUTTON1) = IDYES;
			if res = False then
			begin
				Result := False;
			end
			else
			begin
				Result := False;
				ShellExec('open', 'http://download.microsoft.com/download/5/6/7/567758a3-759e-473e-bf8f-52154438565a/dotnetfx.exe', '', '', SW_SHOWNORMAL, ewNoWait, errRes);
			end;
		end;
	end;
end;

[CustomMessages]
noDotNetInstalled =LitePM {#MyAppVersion} requires Microsoft .Net Framework 4.8 to work. Please install .Net Framework 4.8 and then try to install LitePM again. Would you like to download .Net Framework 4.8 now ?
OtherTasks =Other tasks
RestoreTaskmgr =Restore Windows task manager
ReplaceTaskmgr =Replace Windows task manager
Optimization =Optimizing Yet Another (remote) Process Monitor...
StartServer=Start LitePM server
HelpFile=Help







